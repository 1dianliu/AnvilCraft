plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    alias libs.plugins.modDevGradle
    alias libs.plugins.spotless
    alias libs.plugins.lombok
    alias libs.plugins.machete
}

String buildType = 'build'
String buildNumber
if (System.getenv("CI_BUILD") == 'false') buildNumber = null
else {
    if (System.getenv("PR_BUILD") != 'false') buildType = 'pr'
    buildNumber = System.getenv("GITHUB_RUN_NUMBER")
}
version = "${mod_version}" + (buildNumber != null ? "+${buildType}.${buildNumber}" : "")
group = maven_group

apply from: "$rootDir/gradle/scripts/moddevgradle.gradle"
apply from: "$rootDir/gradle/scripts/repositories.gradle"
apply from: "$rootDir/gradle/scripts/jars.gradle"
apply from: "$rootDir/gradle/scripts/publishing.gradle"
apply from: "$rootDir/gradle/scripts/resources.gradle"
apply from: "$rootDir/gradle/scripts/spotless.gradle"
apply from: "$rootDir/dependencies.gradle"

// Mojang ships Java 21 to end users starting in 1.20.5, so mods should target Java 21.
java.toolchain.languageVersion = JavaLanguageVersion.of(21)

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

compileJava.options.encoding = 'UTF-8'

machete {
    // disable machete locally for faster builds
    enabled = false
}

lombok {
    version = "1.18.34"
}
